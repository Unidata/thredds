apply plugin: 'war'

dependencies {
  compile project(':bufr')
  compile project(':cdm')
  compile project(':clcommon')
  compile project(':grib')
  compile project(':netcdf4')
  compile project(':opendap')
  compile (project(':tdcommon')) {
    exclude group: "commons-logging"
  }
  compile project(':visad')
  compile project(':waterml')

  compile project(':dap4:d4servletshared')

  compile ('com.coverity.security:coverity-escapers:1.1.1')

  compile group: 'org.springframework', name: 'spring-context', version:springVersion
  compile group: 'org.springframework', name: 'spring-beans', version:springVersion
  compile group: 'org.springframework', name: 'spring-webmvc', version:springVersion

  compile group: 'commons-fileupload', name: 'commons-fileupload', version: "1.2.2"
  compile group: 'org.apache.commons', name: 'commons-lang3', version: commonsLang3Version
  compile group: 'commons-io', name: 'commons-io', version: commonsIoVersion

  // plug-in
  runtime (group: 'EDS', name: 'threddsIso', version: "2.2.4")

  // ncss
  // JSR 303 with Hibernate Validator
  compile group: 'javax.validation', name: 'validation-api', version: "1.0.0.GA"
  compile group: 'org.hibernate', name: 'hibernate-validator', version: "4.3.1.Final"
  compile (group: 'org.jfree', name: 'jfreechart', version: jfreechartVersion) {
    exclude group: 'com.lowagie'
  }

  // wms
  compile (group: 'uk.ac.rdg.resc', name: 'ncwms', version: "1.2.tds.4.5.4") {
    exclude group: 'commons-logging'
    exclude group: 'edu.ucar'
    exclude group: 'joda-time'
    exclude group: 'org.jfree'
    exclude group: 'org.springframework'
    exclude group: 'org.slf4j'
  }
  compile group: 'oro', name: 'oro', version: "2.0.8"

  providedCompile group: 'javax.servlet', name: 'javax.servlet-api', version:servletVersion
  testCompile project(':cdm').sourceSets.test.output
  testCompile group: 'org.springframework', name: 'spring-test', version:springVersion
  testCompile group: 'httpunit', name: 'httpunit', version:'1.7'

  // logging
  runtime group: 'org.apache.logging.log4j', name: 'log4j-core', version: log4jVersion
  runtime group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: log4jVersion
  runtime group: 'org.slf4j', name: 'jcl-over-slf4j', version: slf4jVersion
}

war {
  manifest {
    attributes 'Implementation-Version': version,
            'Implementation-Title': 'THREDDS Data Server (TDS)',
            'Implementation-Vendor': 'UCAR/Unidata',
            'Implementation-Vendor-Id': 'edu.ucar',
            'Built-On': new Date(),
            'Build-Jdk': System.properties['java.version']
  }

}

task printProps << {
  println System.properties['java.version'];
  for (prop in System.properties.propertyNames()) printf("%s = %s%n", prop, System.properties[prop])
}

