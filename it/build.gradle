description = "TDS - NetCDF-Java library integration Test module. This module contains all classes and settings " +
        "needed to test the NetCDF-Java library in a servlet container. Starts up a TDS server and then sends " +
        "requests to it. Relies on having access to cdmUnitTest directory, so can only be run at Unidata."
ext.title = "Test Integration"
ext.url = "http://www.unidata.ucar.edu/software/thredds/current/tds/TDS.html"

apply from: "$rootDir/gradle/any/dependencies.gradle"
apply from: "$rootDir/gradle/any/java.gradle"
apply from: "$rootDir/gradle/any/testing.gradle"
apply from: "$rootDir/gradle/any/coverage.gradle"
// 'it' is not published
apply from: "$rootDir/gradle/any/gretty.gradle"

dependencies {
    testCompile project(":cdm")
    testCompile project(":httpservices")
    testCompile project(":tdcommon")
    testCompile project(":tds")
    
    testRuntime project(":clcommon")
    testRuntime project(":grib")
    testRuntime project(":opendap")
    testRuntime project(":visadCdm")
    testRuntime project(':dap4:d4cdm')
    
    testCompile libraries["jdom2"]
    testCompile libraries["commons-io"]
    testCompile libraries["joda-time"]
    testCompile libraries["httpcore"]
    testCompile libraries["httpclient"]
    testCompile libraries["commons-lang3"]
    testCompile libraries["spring-context"]
    testCompile libraries["slf4j-api"]
    
    testRuntime libraries["jaxen"]
    
    // Unlike the other subprojects, we do not need to add an SLF4J binding to testRuntime;
    // we're overlaying tds (see below), so we're already getting the binding that it declares.
}

// Gretty will start the webapp before this task and shut it down afterward.
test {
    // isContentRootAvailable, isJenkins, and contentRootKey are defined on the root project in testing.gradle.
    if (!isContentRootAvailable && !isJenkins) {  // Don't skip tests on Jenkins, except NotJenkins ones.
        logger.warn "Skipping all integration tests (task \'$path\'): " +
                "\'$contentRootKey\' property not defined."

        // We can't stand up a functioning TDS test instance without a valid content root,
        // so disable the task altogether.
        enabled = false
    }
}

// Additional Gretty config specific to integration testing.
gretty {
    overlayAndApplyConfigOf ':tds'
    
    // The normal 'test' task added by the Java plugin has nothing to do in this subproject, because every single
    // test requires a running TDS. Therefore, we're repurposing the 'test' task for integration testing.
    integrationTestTask = 'test'
}
