description = "The NetCDF-Java Library is a Java interface to NetCDF files, as well as to many other types of " +
        "scientific data formats."
ext.title = "CDM core library"

apply plugin: 'groovy'  // For Spock tests.

dependencies {
    compile project(':udunits')
    compile project(':httpservices')

    compile libraries["httpcore"]
    compile libraries["joda-time"]
    compile libraries["jdom2"]
    compile libraries["jcip-annotations"]
    compile libraries["quartz"]
    compile libraries["protobuf-java"]
    compile libraries["guava"]
    compile libraries["jcommander"]
    compile libraries["aws-java-sdk-s3"]  // For CrawlableDatasetAmazonS3.
    compile libraries["jackson-core"]  // Replace what was in aws-java-sdk-s3
    compile libraries["jackson-annotations"]  // Replace what was in aws-java-sdk-s3
    compile libraries["jackson-databind"]  // Replace what was in aws-java-sdk-s3

    compile libraries["slf4j-api"]

    // These 4 are all for Spock.
    testCompile libraries["spock-core"]
    testCompile libraries["groovy-all"]
    testRuntime libraries["cglib-nodep"]
    testRuntime libraries["objenesis"]
}

javadoc {
    title = 'Public javadoc for netcdf-java version ' + version
    source = fileTree(dir: 'src/main/java').matching {
        include 'thredds/catalog/*.java'
        include 'ucar/ma2/*.java'
        include 'ucar/nc2/*.java'
        include 'ucar/nc2/constants/*.java'
        include 'ucar/nc2/dataset/**/*.java'
        include 'ucar/nc2/dt/*.java'
        include 'ucar/nc2/dt/grid/*.java'
        include 'ucar/nc2/ft/*.java'
        include 'ucar/nc2/ncml/*.java'
        include 'ucar/nc2/time/*.java'
        include 'ucar/nc2/units/*.java'
        include 'ucar/nc2/util/*.java'
        include 'ucar/unidata/**/*.java'
    }
}

// TODO: Use the Sync task for this.
task releaseDocs(dependsOn: javadoc, group: 'Release') << {
    String releaseDir = new File(webdir, "javadoc").absolutePath

    ant.delete(dir: releaseDir)
    ant.mkdir(dir: releaseDir)

    copy {
        println "copyDocs to " + releaseDir
        from("build/docs/javadoc")
        into releaseDir
    }
}

jar.manifest.attributes 'Main-Class': 'ucar.nc2.NCdumpW'
