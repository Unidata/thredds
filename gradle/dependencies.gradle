// Gradle dependency managment.
// This technique was inspired by: http://stackoverflow.com/questions/9547170

//================================================ Repositories ================================================//

allprojects {  // Doesn't apply any plugins: safe to run closure on all projects.
    // TODO: Specify preferred repository with each dependency definition (although, have a sensible default).
    // Then, based on the declared dependencies, automatically add the necessary repos, and ONLY the necessary repos.
    repositories {
        mavenCentral()
        maven {
            // JitPack builds GitHub projects on demand and publishes ready-to-use packages.
            // We need this because Spock requires CGLIB 3.2+ in order to work on Java 8.
            // However, as of 2015-08-26, v3.2 hasn't been released, even though working code exists on master.
            // So, using JitPack, we're going to grab an appropriate CGLIB artifact.
            // See https://github.com/cglib/cglib/issues/32
            url "https://jitpack.io"  // For libraries["cglib-nodep"]
        }
        maven {
            url "https://artifacts.unidata.ucar.edu/content/repositories/unidata"
        }
        maven {
            url "https://artifacts.unidata.ucar.edu/content/repositories/unidata-3rdparty/"
        }
    }
}

//================================================ Dependencies ================================================//

ext {
    // Extra properties must be declared in the "ext" namespace. After declaration, they can be used without prefix.
    // These will be inherited by subprojects.
    versions = [:]
    libraries = [:]
}

////////////////////////////////////////// Plugins //////////////////////////////////////////

libraries["gretty"] = "org.akhikhl.gretty:gretty:1.2.0"

libraries["shadow"] = "com.github.jengelman.gradle.plugins:shadow:1.2.2"

libraries["coveralls-gradle-plugin"] = "org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.3.1"

libraries["gradle-extra-configurations-plugin"] = "com.netflix.nebula:gradle-extra-configurations-plugin:2.2.1"
 
////////////////////////////////////////// Spring //////////////////////////////////////////

versions["spring"] = "4.2.3.RELEASE"

libraries["spring-core"] = "org.springframework:spring-core:${versions["spring"]}"

libraries["spring-context"] = "org.springframework:spring-context:${versions["spring"]}"

libraries["spring-beans"] = "org.springframework:spring-beans:${versions["spring"]}"

libraries["spring-web"] = "org.springframework:spring-web:${versions["spring"]}"

libraries["spring-webmvc"] = "org.springframework:spring-webmvc:${versions["spring"]}"

////////////////////////////////////////// Spring security //////////////////////////////////////////

versions["spring-security"] = "4.0.3.RELEASE"

libraries["spring-security-core"] = "org.springframework.security:spring-security-core:${versions["spring-security"]}"

libraries["spring-security-config"] = "org.springframework.security:spring-security-config:${versions["spring-security"]}"

libraries["spring-security-web"] = "org.springframework.security:spring-security-web:${versions["spring-security"]}"

// "spring-security-taglibs" is only used in the Maven build of wmotables.
// wmotables is not built in Gradle, but I'm leaving this here so that
// dependencies.gradle matches the root POM's dependencyManagement section as closely as possible.
libraries["spring-security-taglibs"] = "org.springframework.security:spring-security-taglibs:${versions["spring-security"]}"

////////////////////////////////////////// XML Beans //////////////////////////////////////////

libraries["xmlbeans"] = "org.apache.xmlbeans:xmlbeans:2.6.0"

libraries["52n-oxf-xmlbeans"] = "org.n52.sensorweb:52n-oxf-xmlbeans:2.0.0-alpha.3.3"

versions["52n-common-xml"] = "2.1.0"

libraries["52n-xml-waterML-v20"] = "org.n52.sensorweb:52n-xml-waterML-v20:${versions["52n-common-xml"]}"

libraries["52n-xml-gml-v321"] = "org.n52.sensorweb:52n-xml-gml-v321:${versions["52n-common-xml"]}"

libraries["52n-xml-sweCommon-v20"] = "org.n52.sensorweb:52n-xml-sweCommon-v20:${versions["52n-common-xml"]}"

libraries["52n-xml-om-v20"] = "org.n52.sensorweb:52n-xml-om-v20:${versions["52n-common-xml"]}"

libraries["52n-xml-sampling-v20"] = "org.n52.sensorweb:52n-xml-sampling-v20:${versions["52n-common-xml"]}"

////////////////////////////////////////// Logging //////////////////////////////////////////

versions["slf4j"] = "1.7.13"

libraries["slf4j-api"] = "org.slf4j:slf4j-api:${versions["slf4j"]}"

libraries["slf4j-jdk14"] = "org.slf4j:slf4j-jdk14:${versions["slf4j"]}"

libraries["jcl-over-slf4j"] = "org.slf4j:jcl-over-slf4j:${versions["slf4j"]}"

versions["log4j"] = "2.4.1"

libraries["log4j-api"] = "org.apache.logging.log4j:log4j-api:${versions["log4j"]}"

libraries["log4j-core"] = "org.apache.logging.log4j:log4j-core:${versions["log4j"]}"

libraries["log4j-web"] = "org.apache.logging.log4j:log4j-web:${versions["log4j"]}"

libraries["log4j-slf4j-impl"] = "org.apache.logging.log4j:log4j-slf4j-impl:${versions["log4j"]}"

////////////////////////////////////////// Testing //////////////////////////////////////////

libraries["junit"] = "junit:junit:4.12"

libraries["spring-test"] = "org.springframework:spring-test:${versions["spring"]}"

libraries["hamcrest-core"] = "org.hamcrest:hamcrest-core:1.3"

libraries["com.eclipsesource.restfuse"] = "com.restfuse:com.eclipsesource.restfuse:1.0.0"

libraries["httpunit"] = "httpunit:httpunit:1.7"

libraries["commons-io"] = "commons-io:commons-io:2.4"
libraries["commons-compress"] = "org.apache.commons:commons-compress:1.8.1"

libraries["equalsverifier"] = "nl.jqno.equalsverifier:equalsverifier:1.7.5"

// These four are all for Spock.
libraries["spock-core"] = "org.spockframework:spock-core:1.0-groovy-2.4"

libraries["groovy-all"] = "org.codehaus.groovy:groovy-all:2.4.5"

// In Spock, allows mocking of classes (in addition to interfaces).
// 5503bcca74 is the most recent commit on the cglib master branch as of 2015-08-26.
libraries["cglib-nodep"] = "com.github.cglib.cglib:cglib-nodep:5503bcca74"

// In Spock, allows mocking of classes without default constructor (together with CGLIB).
libraries["objenesis"] = "org.objenesis:objenesis:2.2"


////////////////////////////////////////// Other //////////////////////////////////////////

// See https://github.com/coverity/coverity-security-library
libraries["coverity-escapers"] = "com.coverity.security:coverity-escapers:1.1.1"

libraries["guava"] = "com.google.guava:guava:18.0"

libraries["httpclient"] = "org.apache.httpcomponents:httpclient:4.5.1"

libraries["httpcore"] = "org.apache.httpcomponents:httpcore:4.4.4"

libraries["httpmime"] = "org.apache.httpcomponents:httpmime:4.5.1"

// Immutable annotation
libraries["jcip-annotations"] = "net.jcip:jcip-annotations:1.0"

// command line parser
libraries["jcommander"] = "com.beust:jcommander:1.35"

libraries["jdom2"] = "org.jdom:jdom2:2.0.6"

// used by FMRC - replace ?
libraries["je"] = "com.sleepycat:je:4.0.92"

libraries["jna"] = "net.java.dev.jna:jna:4.2.1"

// replace by javax.time
libraries["joda-time"] = "joda-time:joda-time:2.9.1"

// HTML scraper used in GRIB
libraries["jsoup"] = "org.jsoup:jsoup:1.8.3"

// Nonnull annotation
libraries["jsr305"] = "com.google.code.findbugs:jsr305:3.0.0"

libraries["protobuf-java"] = "com.google.protobuf:protobuf-java:3.0.0-beta-1"

libraries["visad"] = "edu.wisc.ssec:visad:2.0-20130124"

// replace ??
libraries["commons-lang3"] = "org.apache.commons:commons-lang3:3.3.2"

// GRIB
libraries["jj2000"] = "edu.ucar:jj2000:5.3"

////////////////////////////// TDS ///////////////////////////////////////////

libraries["aws-java-sdk-s3"] = "com.amazonaws:aws-java-sdk-s3:1.10.11"

libraries["chronicle-map"] = "net.openhft:chronicle-map:2.3.9"

libraries["javax.servlet-api"] = "javax.servlet:javax.servlet-api:3.1.0"

libraries["jstl-api"] = "org.apache.taglibs:taglibs-standard-spec:1.2.5"
libraries["jstl-impl"] = "org.apache.taglibs:taglibs-standard-impl:1.2.5"

libraries["jaxen"] = "jaxen:jaxen:1.1.4"

libraries["oro"] = "oro:oro:2.0.6"

versions["edal"] = "1.1.1"

libraries["edal-common"] = "uk.ac.rdg.resc:edal-common:${versions["edal"]}"

libraries["edal-cdm"] = dependencies.create("uk.ac.rdg.resc:edal-cdm:${versions["edal"]}") {
    // These are dependencies that are included in edal-cdm, but are provided by
    // us already, so we will exclude these from the edal-cdm artifact.
    exclude group: 'edu.ucar', module: 'grib'
    exclude group: 'edu.ucar', module: 'netcdf4'
    exclude group: 'edu.ucar', module: 'opendap'
    exclude group: 'com.sleepycat', module: 'je'
    exclude group: 'oro', module: 'oro'
}

libraries["edal-wms"] = "uk.ac.rdg.resc:edal-wms:${versions["edal"]}"

libraries["edal-graphics"] = "uk.ac.rdg.resc:edal-graphics:${versions["edal"]}"

libraries["edal-godiva"] = dependencies.create("uk.ac.rdg.resc:edal-godiva:${versions["edal"]}") {
    // In edal-godiva's POM, this should have been marked "provided" instead of "compile", because it's
    // not needed at runtime. We have to exclude it ourselves as a result.
    exclude module: 'gwt-user'
}

libraries["saxon-he"] = "net.sf.saxon:Saxon-HE:9.6.0-7"

libraries["quartz"] = "org.quartz-scheduler:quartz:2.2.2"

libraries["threddsIso"] = "EDS:threddsIso:2.2.8"

versions["gwt"] = "2.7.0"

libraries["gwt-user"] =  "com.google.gwt:gwt-user:${versions["gwt"]}"

libraries["gwt-dev"] = "com.google.gwt:gwt-dev:${versions["gwt"]}"

/////////////////////////////////////////////////////////////////////////////////
// Bean validation 1.1, used by TDS

libraries["validation-api"] = "javax.validation:validation-api:1.1.0.Final"
libraries["hibernate-validator"] = "org.hibernate:hibernate-validator:5.2.2.Final"
libraries["javax.el-api"] = "javax.el:javax.el-api:2.2.4"
libraries["javax.el"] = "org.glassfish.web:javax.el:2.2.4"


////////////////////////////// Testing ///////////////////////////////////////////

//libraries["commons-fileupload"] = "commons-fileupload:commons-fileupload:1.2.2"

//libraries["geoapi-pending"] = "org.opengis:geoapi-pending:3.1-M04"

//libraries["oro"] = "oro:oro:2.0.8"

////////////////////////////////////////// ToolsUI //////////////////////////////////////////

libraries["bounce"] = "org.bounce:bounce:0.14"

// Old and crufty. Only used by TdsMonitor in :ui. Can we switch to Guava caches?
// libraries["ehcache-core"] = "net.sf.ehcache:ehcache-core:2.6.2"

libraries["imgscalr-lib"] = "org.imgscalr:imgscalr-lib:4.2"

libraries["jcommon"] = "org.jfree:jcommon:1.0.23"

libraries["jfreechart"] = "org.jfree:jfreechart:1.0.19"

libraries["jgoodies-forms"] = "com.jgoodies:jgoodies-forms:1.6.0"

//////////////////////////// Transitive dependency replacements and exclusions ////////////////////////////

allprojects {  // Doesn't apply any plugins: safe to run closure on all projects.
    // Executes the given closure against all objects in this collection, and any objects subsequently added to this
    // collection. See org.gradle.api.DomainObjectCollection.all(Closure)
    configurations.all {
        // Replace every instance of "commons-logging" in the dependency tree with "jcl-over-slf4j".
        // This effectively converts every jcl call to an slf4j call.
        // "commons-logging" is dragged in by spring-core and httpclient.
        resolutionStrategy.eachDependency { DependencyResolveDetails dep ->
            if (dep.requested.name == 'commons-logging') {
                dep.useTarget libraries["jcl-over-slf4j"]
            }
        }

        // STAX comes with Java 1.6+
        // Dragged in by xmlbeans
        exclude group: 'stax', module: 'stax-api'

        // Xerces comes with Java 1.6+
        // This SHOULD be okay, but see http://stackoverflow.com/questions/1167757
        // Dragged in by 52n-oxf-xmlbeans
        exclude group: 'xerces', module: 'xercesImpl'

        // This is an old version of the servlet API: v2. We're using v3.
        // Dragged in by httpunit.
        exclude group: 'javax.servlet', module: 'servlet-api'

        // Includes a lot of classes that already come with Java 1.6+
        // and we don't use the "tidy" functionality (i.e. HTML pretty printing) anyway.
        // Dragged in by httpunit.
        exclude group: 'jtidy', module: 'jtidy'

        // On TDS startup, I'm getting "Class path contains multiple SLF4J bindings" warnings.
        // This is one of those bindings. Strangely, I have NO idea what dependency is dragging it in.
        // It doesn't matter though; we always want to see the logging messages.
        exclude group: 'org.slf4j', module: 'slf4j-nop'

        // Another SLF4J binding that we don't want. Dragged in by edal-common.
        exclude group: 'org.slf4j', module: 'slf4j-log4j12'
    }
}
