// Gradle dependency management.
// This technique was inspired by: http://stackoverflow.com/questions/9547170

//================================================ Repositories ================================================//

repositories {
    // Prefer JCenter to Maven Central. See
    // https://blog.bintray.com/2015/02/09/android-studio-migration-from-maven-central-to-jcenter/
    jcenter()
    mavenCentral()  // JCenter isn't quite a superset of Maven Central.
    
    // All of the hosted repositories below could be replaced with:
    //     url "https://artifacts.unidata.ucar.edu/repository/unidata-all/"
    // which is a group repository that contains all other repositories. However, I prefer to list all source
    // repos explicitly so that we know where all artifacts ultimately come from.
    
    // Hosted release repositories.
    maven {
        // For "threddsIso", "visad" and "jj2000".
        url "https://artifacts.unidata.ucar.edu/repository/unidata-releases/"
    }
    maven {
        // For snapshots of "threddsIso"
        url "https://artifacts.unidata.ucar.edu/repository/unidata-snapshots/"
    }
    maven {
        // For "bounce", which is used in :ui. As of 5.0.0, it is the ONLY dependency from this repo that we
        // still use. We should really try to eliminate our use of it.
        url "https://artifacts.unidata.ucar.edu/repository/unidata-3rdparty/"
    }
    
    // Third-party repositories.
    maven {
        url "https://dl.bintray.com/cwardgar/maven/"  // For "gretty".
    }

    // edal-java SNAPSHOTS
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
}

//================================================ Dependencies ================================================//

ext {
    // Extra properties must be declared in the "ext" namespace. After declaration, they can be used without prefix.
    // These will be inherited by subprojects.
    versions = [:]
    libraries = [:]
}

////////////////////////////////////////// Plugins //////////////////////////////////////////

libraries["gretty"] = "com.cwardgar.gretty-fork:gretty:2.0.1"

libraries["shadow"] = "com.github.jengelman.gradle.plugins:shadow:1.2.3"

libraries["coveralls-gradle-plugin"] = "org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.6.3"

libraries["sonarqube-gradle-plugin"] = "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.7"

libraries["license-gradle-plugin"] = "gradle.plugin.nl.javadude.gradle.plugins:license-gradle-plugin:0.13.1"

libraries["jruby-gradle-plugin"] = "com.github.jruby-gradle:jruby-gradle-plugin:1.5.0"

////////////////////////////////////////// Spring //////////////////////////////////////////

versions["spring"] = "4.3.20.RELEASE"

libraries["spring-core"] = "org.springframework:spring-core:${versions["spring"]}"

libraries["spring-context"] = "org.springframework:spring-context:${versions["spring"]}"

libraries["spring-beans"] = "org.springframework:spring-beans:${versions["spring"]}"

libraries["spring-web"] = "org.springframework:spring-web:${versions["spring"]}"

libraries["spring-webmvc"] = "org.springframework:spring-webmvc:${versions["spring"]}"

////////////////////////////////////////// Spring security //////////////////////////////////////////

versions["spring-security"] = "4.2.12.RELEASE"

libraries["spring-security-config"] = "org.springframework.security:spring-security-config:${versions["spring-security"]}"

libraries["spring-security-web"] = "org.springframework.security:spring-security-web:${versions["spring-security"]}"

////////////////////////////////////////// XML Beans //////////////////////////////////////////

libraries["xmlbeans"] = "org.apache.xmlbeans:xmlbeans:2.6.0"

versions["52n-common-xml"] = "2.2.0"

libraries["52n-xml-waterML-v20"] = "org.n52.sensorweb:52n-xml-waterML-v20:${versions["52n-common-xml"]}"

libraries["52n-xml-gml-v321"] = "org.n52.sensorweb:52n-xml-gml-v321:${versions["52n-common-xml"]}"

libraries["52n-xml-sweCommon-v20"] = "org.n52.sensorweb:52n-xml-sweCommon-v20:${versions["52n-common-xml"]}"

libraries["52n-xml-om-v20"] = "org.n52.sensorweb:52n-xml-om-v20:${versions["52n-common-xml"]}"

libraries["52n-xml-sampling-v20"] = "org.n52.sensorweb:52n-xml-sampling-v20:${versions["52n-common-xml"]}"

////////////////////////////////////////// Logging //////////////////////////////////////////

versions["slf4j"] = "1.7.25"

libraries["slf4j-api"] = "org.slf4j:slf4j-api:${versions["slf4j"]}"

libraries["jcl-over-slf4j"] = "org.slf4j:jcl-over-slf4j:${versions["slf4j"]}"

versions["log4j"] = "2.11.0"

libraries["log4j-web"] = "org.apache.logging.log4j:log4j-web:${versions["log4j"]}"

libraries["log4j-slf4j-impl"] = "org.apache.logging.log4j:log4j-slf4j-impl:${versions["log4j"]}"

libraries["logback-classic"] = "ch.qos.logback:logback-classic:1.2.3"

////////////////////////////////////////// Testing //////////////////////////////////////////

libraries["junit"] = "junit:junit:4.12"

libraries["JUnitParams"] = "pl.pragmatists:JUnitParams:1.0.5"

libraries["spring-test"] = "org.springframework:spring-test:${versions["spring"]}"

libraries["hamcrest-core"] = "org.hamcrest:hamcrest-core:1.3"

libraries["commons-io"] = "commons-io:commons-io:2.5"

libraries["commons-compress"] = "org.apache.commons:commons-compress:1.12"

libraries["xmlunit-core"] = "org.xmlunit:xmlunit-core:2.3.0"

// These four are all for Spock.
libraries["spock-core"] = "org.spockframework:spock-core:1.0-groovy-2.4"

libraries["groovy-all"] = "org.codehaus.groovy:groovy-all:2.4.7"

// In Spock, allows mocking of classes (in addition to interfaces).
libraries["cglib-nodep"] = "cglib:cglib-nodep:3.2.4"

// In Spock, allows mocking of classes without default constructor (together with CGLIB).
libraries["objenesis"] = "org.objenesis:objenesis:2.4"

//mockito
libraries["mockito"] = "org.mockito:mockito-core:2.+"

// Fluent assertions for Java
libraries["truth"] = "com.google.truth:truth:0.44"


////////////////////////////////////////// Other //////////////////////////////////////////

// See https://github.com/coverity/coverity-security-library
libraries["coverity-escapers"] = "com.coverity.security:coverity-escapers:1.1.1"

libraries["guava"] = "com.google.guava:guava:27.0.1-jre"

libraries["httpclient"] = "org.apache.httpcomponents:httpclient:4.5.2"

libraries["httpcore"] = "org.apache.httpcomponents:httpcore:4.4.5"

libraries["httpmime"] = "org.apache.httpcomponents:httpmime:4.5.2"

// command line parser
libraries["jcommander"] = "com.beust:jcommander:1.48"

libraries["jdom2"] = "org.jdom:jdom2:2.0.6"

// used by FMRC - replace ?
// Latest version is 5.0.73. Can we upgrade?
libraries["je"] = "com.sleepycat:je:4.0.92"

libraries["jna"] = "net.java.dev.jna:jna:4.2.2"

// replaces the one that we exclude from chronicle-map
libraries["jna-platform"] = "net.java.dev.jna:jna-platform:4.2.2"

// replace by javax.time
libraries["joda-time"] = "joda-time:joda-time:2.9.4"

// HTML scraper used in GRIB
libraries["jsoup"] = "org.jsoup:jsoup:1.11.2"

// Annotations
libraries["jsr305"] = "com.google.code.findbugs:jsr305:3.0.2"

libraries["protobuf-java"] = "com.google.protobuf:protobuf-java:3.6.1"

libraries["visad"] = "edu.wisc.ssec:visad:2.0-20130124"

// replace ??
libraries["commons-lang3"] = "org.apache.commons:commons-lang3:3.4"

// GRIB
libraries["jj2000"] = "edu.ucar:jj2000:5.3"

// The easy HTTP client for Groovy (and Java). Used in buildSrc.
libraries["http-builder-ng-okhttp"] = "io.github.http-builder-ng:http-builder-ng-okhttp:1.0.3"

////////////////////////////// TDS ///////////////////////////////////////////

libraries["aws-java-sdk-s3"] = dependencies.create("com.amazonaws:aws-java-sdk-s3:1.11.236") {
    // exclude jackson deps so that they can be overridden
    // with 2.7.x deps to address security issue. See
    // https://github.com/aws/aws-sdk-java/issues/1159
    // as of the time these were added, the versions of these were:
    // jackson-core: 2.6.7
    // jackson-annotations: 2.6.0
    // jackson-databind: 2.6.7.1
    exclude group: 'com.fasterxml.jackson.core', module: 'jackson-core'
    exclude group: 'com.fasterxml.jackson.core', module: 'jackson-annotations'
    exclude group: 'com.fasterxml.jackson.core', module: 'jackson-databind'
}

// replace the jackson.core libs that were excluded from aws-java-sdk-s3
libraries["jackson-core"] = "com.fasterxml.jackson.core:jackson-core:2.9.9"
libraries["jackson-annotations"] = "com.fasterxml.jackson.core:jackson-annotations:2.9.9"
libraries["jackson-databind"] = "com.fasterxml.jackson.core:jackson-databind:2.9.9"

libraries["chronicle-map"] = dependencies.create("net.openhft:chronicle-map:3.17.0") {
    // the version of jna shipped with chronicle-map requires GLIBC 2.14
    // which prevents the use of the TDS on several versions of linux, such
    // as CentOS and Hed Hat 6. We already include jna in netCDF-Java for
    // netCDF-C support, so we will rely on that version instead.
    // We should be able to get rid of this on the next release of chronicle-map,
    // as their snapshot BOM pom shows that they pulled back on jna.
    exclude group: 'net.java.dev.jna', module: 'jna'
    exclude group: 'net.java.dev.jna', module: 'jna-platform'
    // We do not need XStream for our use of chronicle-map.
    // Nuke it. [SEC]
    exclude group: 'com.thoughtworks.xstream', module: 'xstream'
    // We do not need pax-url-aether
    // Nuke it. [SEC]
    exclude group: 'org.ops4j.pax.url', module: 'pax-url-aether'
}

libraries["javax.servlet-api"] = "javax.servlet:javax.servlet-api:3.1.0"

versions["taglibs"] = "1.2.5"

libraries["taglibs-standard-spec"] = "org.apache.taglibs:taglibs-standard-spec:${versions["taglibs"]}"

libraries["taglibs-standard-impl"] = "org.apache.taglibs:taglibs-standard-impl:${versions["taglibs"]}"

libraries["jaxen"] = "jaxen:jaxen:1.1.6"

libraries["oro"] = "oro:oro:2.0.8"

libraries["thymeleaf-spring4"] = "org.thymeleaf:thymeleaf-spring4:3.0.11.RELEASE"

versions["edal"] = "1.4.2-SNAPSHOT"

// needed for edal-common but this pulls in a newer version.
libraries["ehcache"] = "net.sf.ehcache:ehcache:2.10.6"

libraries["edal-common"] = dependencies.create("uk.ac.rdg.resc:edal-common:${versions["edal"]}") {
    // exclude ehcache from this dep and use the more recent version above
    exclude group: 'net.sf.ehcache', module: 'net.sf.ehcache:ehcache'
}

libraries["edal-cdm"] = dependencies.create("uk.ac.rdg.resc:edal-cdm:${versions["edal"]}") {
    // These are dependencies that are included in edal-cdm, but are provided by
    // us already, so we will exclude these from the edal-cdm artifact.
    exclude group: 'edu.ucar', module: 'cdm'
    exclude group: 'edu.ucar', module: 'udunits'
    exclude group: 'edu.ucar', module: 'netcdf4'
    exclude group: 'edu.ucar', module: 'grib'
    exclude group: 'edu.ucar', module: 'opendap'
    exclude group: 'com.sleepycat', module: 'je'
    exclude group: 'oro', module: 'oro'
}

libraries["edal-wms"] = "uk.ac.rdg.resc:edal-wms:${versions["edal"]}"

libraries["edal-graphics"] = "uk.ac.rdg.resc:edal-graphics:${versions["edal"]}"

libraries["edal-godiva"] = dependencies.create("uk.ac.rdg.resc:edal-godiva:${versions["edal"]}") {
    // In edal-godiva's POM, this should have been marked "provided" instead of "compile", because it's
    // not needed at runtime. We have to exclude it ourselves as a result.
    exclude module: 'gwt-user'
}

versions["gwt"] = "2.7.0"

libraries["gwt-user"] =  "com.google.gwt:gwt-user:${versions["gwt"]}"

libraries["gwt-dev"] = "com.google.gwt:gwt-dev:${versions["gwt"]}"

libraries["threddsIso"] = "EDS:threddsIso:2.3.0-SNAPSHOT"

libraries["Saxon-HE"] = "net.sf.saxon:Saxon-HE:9.7.0-8"

libraries["quartz"] = "org.quartz-scheduler:quartz:2.3.1"

// Version update note:
// spring-context 4.x is limited to hibernate-validator 4.3.2.Final. We can't update to hibernate-validator 5.x
// hibernate-validator 4.3.2.Final is limited to validation-api 1.0.0.GA. We can't update to validation-api 1.1.0.Final

libraries["hibernate-validator"] = "org.hibernate:hibernate-validator:4.3.2.Final"

libraries["validation-api"] = "javax.validation:validation-api:1.0.0.GA"

// used for writing json-LD

libraries["json"] = 'org.json:json:20180813'

////////////////////////////////////////// ToolsUI //////////////////////////////////////////

// A set of Java and Swing extensions in NcmlEditor.
libraries["bounce"] = "org.bounce:bounce:0.14"

// Old and crufty. only used by tdsmonitor in :ui. Can we switch to Guava caches?
// libraries["ehcache-core"] = "net.sf.ehcache:ehcache-core:2.6.2"

libraries["imgscalr-lib"] = "org.imgscalr:imgscalr-lib:4.2"

libraries["jcommon"] = "org.jfree:jcommon:1.0.23"

libraries["jfreechart"] = "org.jfree:jfreechart:1.0.19"

// Latest version is 1.9.0, but there is breakage when we try to upgrade.
libraries["jgoodies-forms"] = "com.jgoodies:jgoodies-forms:1.6.0"

//////////////////////////// Transitive dependency replacements and exclusions ////////////////////////////

// Executes the given closure against all objects in this collection, and any objects subsequently added to this
// collection. See org.gradle.api.DomainObjectCollection.all(Closure)
configurations.all {
    resolutionStrategy {
        // Replace every instance of "commons-logging" in the dependency tree with "jcl-over-slf4j". This effectively
        // converts every JCL call to an slf4j call. We can see the first-level dependencies that drag in
        // "commons-logging" by commenting-out the rule below and running:
        /*
            ./gradlew -q tds:dependencyInsight --configuration runtime --dependency commons-logging
            commons-logging:commons-logging:1.2
            +--- org.apache.httpcomponents:httpclient:4.5.2
            |    ...
            \--- org.springframework:spring-core:4.3.8.RELEASE
                 ...
         */
        eachDependency { DependencyResolveDetails dep ->
            if (dep.requested.name == 'commons-logging') {
                dep.useTarget libraries["jcl-over-slf4j"]
            }
        }
    }
    
    // STAX is already included in Java 1.6+; no need for a third-party depenency.
    /*
        ./gradlew -q tds:dependencyInsight --configuration runtime --dependency stax-api
        stax:stax-api:1.0.1
        +--- org.apache.xmlbeans:xmlbeans:2.6.0
        |    ...
        \--- org.codehaus.jettison:jettison:1.3.7
             \--- net.openhft:chronicle-map:2.4.15
                  ...
     */
    exclude group: 'stax', module: 'stax-api'
    
    // Another SLF4J binding that we don't want.
    /*
        ./gradlew -q tds:dependencyInsight --configuration runtime --dependency slf4j-log4j12
        org.slf4j:slf4j-log4j12:1.7.2
        +--- uk.ac.rdg.resc:edal-common:1.2.7
        |    ...
        \--- uk.ac.rdg.resc:edal-godiva:1.2.7
             ...
     */
    exclude group: 'org.slf4j', module: 'slf4j-log4j12'

}
