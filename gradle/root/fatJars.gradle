// In Gradle, dependencies specified in a parent's buildscript {} block are visible to all children.
// However, that behavior doesn't seem to hold for script plugins (this file) applied from the parent script.
// So we have to repeat ourselves. See the root project's build script for more notes on buildscript {} block weirdness.
buildscript {
    apply from: "$rootDir/gradle/any/dependencies.gradle"

    repositories {
        jcenter()
    }
    dependencies {
        classpath libraries["shadow"]  // We want to import the ShadowJar class.
    }
}

if (name != "thredds") {
    throw new GradleException("This script plugin should only be applied to the root project, not '$name'.")
}

configurations {
    ncIdv
    tdmFat
    netcdfAll
    toolsUI
    dap4lib
    d4ts
}

dependencies {
    ncIdv project(':cdm')
    ncIdv project(':clcommon')
    ncIdv project(':bufr')
    ncIdv project(':grib')
    ncIdv project(':netcdf4')
    ncIdv project(':opendap')
    ncIdv project(':visadCdm')
    ncIdv project(':httpservices')
    ncIdv project(':legacy')

    tdmFat project(':tdm')
    
    netcdfAll project(":cdm")
    netcdfAll project(":clcommon")
    netcdfAll project(":bufr")
    netcdfAll project(":grib")
    netcdfAll project(":netcdf4")
    netcdfAll project(":opendap")
    netcdfAll project(":httpservices")
    netcdfAll project(':visadCdm')  // For Gempak IOSPs.

    toolsUI project(':uicdm')
    
    dap4lib project(':dap4:d4core')
    dap4lib project(':dap4:d4lib')
    dap4lib libraries["logback-classic"]
    
    d4ts project(':dap4:d4ts')
}

apply plugin: 'com.github.johnrengelman.shadow'

import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

def fatJarTasks = []

fatJarTasks << tasks.create(name: 'buildNcIdv', type: ShadowJar) {
    baseName = 'ncIdv'
    configurations = [project.configurations.ncIdv]
    
    dependencies {
        exclude(dependency('c3p0:c3p0'))  // Transitive dependency dragged in by libraries["quartz"]
    }
    
    // Filter out crap from libraries["visad"]
    exclude 'edu/wisc/**'
    exclude 'nom/**'
    exclude 'visad/**'
    
    manifest.attributes 'Implementation-Title': 'ncIdv Module'
}

fatJarTasks << tasks.create(name: 'buildTdmFat', type: ShadowJar) {
    baseName = 'tdmFat'
    configurations = [project.configurations.tdmFat]
    
    doFirst {
        manifest.attributes project(':tdm').tasks.jar.manifest.attributes
    }
}

fatJarTasks << tasks.create(name: 'buildNetcdfAll', type: ShadowJar) {
    baseName = 'netcdfAll'
    configurations = [project.configurations.netcdfAll]
    
    doFirst {
        manifest.attributes project(':cdm').tasks.jar.manifest.attributes
    }
}

fatJarTasks << tasks.create(name: 'buildToolsUI', type: ShadowJar) {
    baseName = 'toolsUI'
    configurations = [project.configurations.toolsUI]
    
    doFirst {
        manifest.attributes project(':uicdm').tasks.jar.manifest.attributes
    }
}

fatJarTasks << tasks.create(name: 'buildDap4Lib', type: ShadowJar) {
    baseName = 'dap4lib'
    configurations = [project.configurations.dap4lib]
    
    doFirst {
        manifest.attributes project(':dap4:d4lib').tasks.jar.manifest.attributes
    }
}

fatJarTasks << tasks.create(name: 'buildD4TS', type: ShadowJar) {
    baseName = 'd4ts'
    configurations = [project.configurations.d4ts]
    
    doFirst {
        manifest.attributes project(':dap4:d4ts').tasks.jar.manifest.attributes
    }
}

// Common configuration.
configure(fatJarTasks) {
    dependsOn configurations*.buildDependencies
    group = "shadow"
    
    // Filter out crap from various other packages.
    exclude 'AUTHORS'
    exclude 'DATE'
    exclude 'LICENCE'
    exclude 'LICENSE'
    exclude 'NOTICE'
    exclude '*.txt'
    exclude 'META-INF/INDEX.LIST'
    exclude 'META-INF/DEPENDENCIES'
    exclude 'META-INF/LICENSE'
    exclude 'META-INF/NOTICE'
    exclude 'META-INF/*.SF'
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
    exclude 'META-INF/*.txt'
    exclude 'META-INF/*.xml'
    
    // Transformations
    append('META-INF/spring.handlers')
    append('META-INF/spring.schemas')
    mergeServiceFiles()
}

// Exclude *.xml files from all fat jars except tdmFat; we need tdm's log4j2.xml to be included.
configure(fatJarTasks - buildTdmFat) {
    exclude '*.xml'
}

// Exclude *.xml files from all fat jars except tdmFat; we need tdm's log4j2.xml to be included.
configure(fatJarTasks - buildTdmFat) {
    exclude '*.xml'
}

// See: https://docs.gradle.org/current/userguide/standard_plugins.html#sec:base_plugins
apply plugin: 'base'

// The base-plugin's "assemble" task automatically creates all artifacts added to the "archives" configuration.
artifacts {
    archives buildNcIdv
    archives buildTdmFat
    archives buildNetcdfAll
    archives buildToolsUI
    archives buildDap4Lib
    archives buildD4TS
}
